buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.2.0"
    }
}

apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "eclipse"
apply plugin: "idea"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

ext {
    gdxVersion = '1.9.11'

    isReleaseBuild = {
        return project.hasProperty("RELEASE")
    }

    getReleaseRepositoryUrl = {
        return project.hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
                : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }

    getSnapshotRepositoryUrl = {
        return project.hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
                : "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    getRepositoryUsername = {
        return project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : "$System.env.NEXUS_USERNAME"
    }

    getRepositoryPassword = {
        return project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : "$System.env.NEXUS_PASSWORD"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

version project.getProperty('version') + (isReleaseBuild() ? "" : "-SNAPSHOT")

dependencies {
    api "com.badlogicgames.gdx:gdx-jnigen-loader:2.2.0"

    testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

ext.getSdl2CFlags = { workingDir ->
	try {
		String executable = 'sdl2-config2';
		if(workingDir != null) {
			executable = workingDir + executable;
			if(!file(executable).exists()) {
				return "";
			}
	    }
	
	    def output = new ByteArrayOutputStream()
	    project.exec {
	        commandLine executable, '--cflags'
	        standardOutput = output
	        ignoreExitValue true
	    }
	    return output.toString().trim();
	} catch(Exception e) {
		return "";
	}
}
ext.getSdl2StaticLibs = { workingDir ->
	try {
		String executable = 'sdl2-config';
		if(workingDir != null) {
			executable = workingDir + executable;
			if(!file(executable).exists()) {
				return "";
			}
	    }
	
	    def output = new ByteArrayOutputStream()
	    project.exec {
	        commandLine executable, '--static-libs'
	        standardOutput = output
	        ignoreExitValue true
	    }
	    return output.toString().trim();
	} catch(Exception e) {
		return "";
	}
}

jnigen {
    sharedLibName = "jamepad"
    all {
        headerDirs = ["../SDL/include"]
        cFlags += " -fvisibility=hidden "
        cppFlags += " -std=c++11 -fvisibility=hidden "
        linkerFlags += " -fvisibility=hidden -fdata-sections -ffunction-sections "
    }
    add(Windows, x32) {
        cFlags += getSdl2CFlags('./SDL/build-windows32/')
        cppFlags += getSdl2CFlags('./SDL/build-windows32/')
        libraries += getSdl2StaticLibs('./SDL/build-windows32/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-windows32/build/.libs/libSDL2.a").absolutePath).replace("-lSDL2main", file("SDL/build-windows32/build/.libs/libSDL2main.a").absolutePath)
    }
    add(Windows, x64) {
        cFlags += getSdl2CFlags('./SDL/build-windows64/')
        cppFlags += getSdl2CFlags('./SDL/build-windows64/')
        libraries += getSdl2StaticLibs('./SDL/build-windows64/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-windows64/build/.libs/libSDL2.a").absolutePath).replace("-lSDL2main", file("SDL/build-windows64/build/.libs/libSDL2main.a").absolutePath)
    }
    add(Linux, x32) {
        cFlags += getSdl2CFlags('./SDL/build-linux32/')
        cppFlags += getSdl2CFlags('./SDL/build-linux32/')
        libraries += getSdl2StaticLibs('./SDL/build-linux32/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-linux32/build/.libs/libSDL2.a").absolutePath)
    }
    add(Linux, x64) {
        cFlags += getSdl2CFlags('./SDL/build-linux64/')
        cppFlags += getSdl2CFlags('./SDL/build-linux64/')
        libraries += getSdl2StaticLibs('./SDL/build-linux64/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-linux64/build/.libs/libSDL2.a").absolutePath)
    }
    add(Linux, x32, ARM) {
        cFlags += getSdl2CFlags('./SDL/build-linuxarm32/')
        cppFlags += getSdl2CFlags('./SDL/build-linuxarm32/')
        libraries += getSdl2StaticLibs('./SDL/build-linuxarm32/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-linuxarm32/build/.libs/libSDL2.a").absolutePath)
    }
    add(Linux, x64, ARM) {
        cFlags += getSdl2CFlags('./SDL/build-linuxarm64/')
        cppFlags += getSdl2CFlags('./SDL/build-linuxarm64/')
        libraries += getSdl2StaticLibs('./SDL/build-linuxarm64/').replaceAll("[a-zA-Z0-9_\\.\\-/]+libSDL2.a", file("SDL/build-linuxarm64/build/.libs/libSDL2.a").absolutePath)
    }
    add(MacOsX, x64) {
        cFlags += getSdl2CFlags('./SDL/build-macos64/')
        cppFlags += getSdl2CFlags('./SDL/build-macos64/')
        libraries += file("SDL/build-macos64/build/.libs/libSDL2.a").absolutePath + " -lm -liconv -Wl,-framework,CoreAudio -Wl,-framework,AudioToolbox -Wl,-framework,ForceFeedback -lobjc -Wl,-framework,CoreVideo -Wl,-framework,Cocoa -Wl,-framework,Carbon -Wl,-framework,IOKit -Wl,-weak_framework,QuartzCore -Wl,-weak_framework,Metal"
    }
    add(MacOsX, x64, ARM) {
        cFlags += getSdl2CFlags('./SDL/build-macosarm64/')
        cppFlags += getSdl2CFlags('./SDL/build-macosarm64/')
        libraries += file("SDL/build-macosarm64/build/.libs/libSDL2.a").absolutePath + " -lm -liconv -Wl,-framework,CoreAudio -Wl,-framework,AudioToolbox -Wl,-framework,ForceFeedback -lobjc -Wl,-framework,CoreVideo -Wl,-framework,Cocoa -Wl,-framework,Carbon -Wl,-framework,IOKit -Wl,-weak_framework,QuartzCore -Wl,-weak_framework,Metal"
    }
}

jar {
    archiveBaseName = "jamepad"
    from fileTree("libs").files
}

dependencies {
    testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}


apply from: 'publish.gradle'
